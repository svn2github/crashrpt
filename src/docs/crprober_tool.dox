/*!

\page crprober_tool Using crprober.exe Tool

<b>Since v1.2.0</b>, CrashRpt provides a tool named <b>crprober.exe</b> that
can be used for automating crash report processing. To run the tool, go to 
\c bin directory and type <b>crprober.exe</b> in the command line.

\note If you run the precompiled tool from \c bin folder and do not compile it yourself, you might
experience some problems if Visual C++ 2005 Redistributable files are not installed on your machine. 
There may be error messages like 'The application configuration is incorrect' and so on.
If you do not have Visual C++ 2005 Redistributable on your machine and do not want to install it, 
please compile the solution yourself as described in \ref compiling_crashrpt.


Note that crprober.exe shouldn't be distributed to end users. They just won't be able
to use it without symbol files.

The tool can be used to open error report files generated by any version of CrashRpt.
However it works the best with reports created by version 1.1 and
higher. 

Typically an error report file contains an associated MD5 hash file. The crprober tool
can perform integrity check using the MD5 hash file. When you receive a report
using HTTP connection, the MD5 hash can be retrieved from 'md5' script argument. When you
receive a report using E-mail, the hash is attached to the mail message. For more information,
see \ref sending_error_reports.

The tool extracts information about the error report (its GUID, version of CrashRpt that
generated the report, creation time in UTC format), the information about the application 
that sent the report (name, version, path to executable file, the list of loaded modules), 
information about the operating system (name, version, service pack, CPU info), and 
information about the exception (exception address, exception code, stack trace). It presents
the extracted information in text form (creates a text file or writes to the terminal).

The error report can't be processed properly without symbol files (*.PDB) generated by Visual Studio for your 
application. You typically store the symbol files locally when preparing to software release.
For more information, see \ref preparing_to_software_release.

The \b crprober.exe is a console tool. It accepts several command line arguments. The
arguments are listed in the table below:
<table>

<tr>
<td> \b Name
<td> \b Description

<tr>
<td> /?
<td> Prints the usage help.

<tr>
<td>/f \<input_file\>     
<td> Required. Absolute or relative path to input ZIP file name.

<tr>
<td>/fmd5 \<md5_file_or_dir\>         
<td>Optional. Path to .md5 file containing MD5 hash for the \<input_file\> or directory name where to 
search for the .md5 file. If this parameter is omitted, the .md5 file is searched in the directory 
where \<input_file\> is located.

<tr>
<td>/o \<out_file_or_dir\>     
<td>Optional. Output file name or directory name. Or use empty name "" to direct output to terminal. 
If this parameter is omitted, output is not generated.

<tr>
<td>/sym \<sym_search_dirs\>   
<td> Optional. Symbol files search directory or list of directories separated with semicolon. 
If this parameter is omitted, symbol files are searched using the default search sequence.

<tr>
<td> /ext \<extract_dir\>
<td> Optional. Specifies the directory where to extract all files contained in error report. 
If this parameter is omitted, files are not extracted.

<tr>
<td> /get \<table_id\> \<column_id\> \<row_id\>
<td> Optional. Specifies the table ID, column ID and row index of the property to retrieve. If 
this parameter specified, the property is written to the output file or to terminal, as defined by /o parameter.
For the list of properties you can retrieve, see \ref using_crashrptprobe_api.
</table>

The crprober tool can return one of the following return codes:

<table>

<tr>
<td> <b> Return Code</b>
<td> \b Meaning

<tr>
<td> 0
<td> OK.

<tr>
<td> 1
<td> Unexpected error.

<tr>
<td> 2
<td> Invalid command line argument. Ensure you pass correct parameters.

<tr>
<td> 3
<td> Integrity check failed (MD5 hash mismatch)

<tr>
<td> 4
<td> Error extracting file. Ensure the extraction path is correct.

</table>

\section crprober_examples Examples of Use

The following example processes the 'error_report.zip' file and writes output to the terminal. Symbol files are
assumed to be located in 'D:\\Symbol Files' directory.
\code
crprober.exe /f error_report.zip /o "" /sym "D:\Symbol Files"
\endcode

The following example opens 'error_report.zip' file located in the current directory, 
performs integrity check using 'error_report.zip.md5' file and writes output to the 'error_report.zip.txt' file.
Symbol files are assumed to be located in 'D:\\Symbol Files' or in 'D:\\MyApp\\sym' directory.
\code
crprober.exe /f error_report.zip /md5 error_report.zip.md5 /o error_report.zip.txt /sym "D:\Symbol Files;D:\MyApp\sym"
\endcode

The following example retrieves the application name from the error report and writes it to terminal:
\code
crprober.exe /f error_report.zip /o "" /sym "D:\Symbol Files;D:\MyApp\sym" /get XmlDescMisc AppName 0
\endcode

The following example retrieves the count of rows in the MdmpModules table and writes it to terminal:
\code
crprober.exe /f error_report.zip /o "" /sym "D:\Symbol Files;D:\MyApp\sym" /get MdmpModules RowCount 0
\endcode


\section crprober_reallife_scenario Real-Life Usage Scenario

Let's assume you receive error reports over E-mail. To automate error reports extraction from E-mail attachments,
you may use some extension of your mail program. For example, if you use Mozilla Thunderbird, you can use 
<a href="http://www.eviljeff.com/?page=moz-extensions">Attachment Extractor extension</a>. You can configure 
the extension to determine the error report messages on the fly and extract email attachments (ZIP and 
MD5 files) to some folder.

When you have ZIP and MD5 files stored in the directory, you may want to process them all at once. You may want to
process reports from the latest version of your application, ignoring all others. You also may want to copy
processed reports to some other directory, leaving not processed reports where they are.
You can automate this task using a batch (BAT) script. 

Below an example batch script is presented. 
- The script processes the files matching the pattern defined by \a INPUT_FILE_PATTERN variable. For example,
"*.zip" pattern means to process all ZIP files in the current directory.
- The script accepts only the reports having application name as defined by \a ACCEPTABLE_APPNAME variable. 
Keep this empty to accept all appnames.
- The script accepts only the reports having application version as defined by \a ACCEPTABLE_APPVERSION variable. 
Keep this empty to accept all app versions.
- The script searches for symbol files in \a SYM_SEARCH_DIRS. This may be semicolon-separated list of directories.
- The script saves processed reports and output text files to \a SAVE_RESULTS_TO_DIR directory.


\code

@echo off
rem Process a group of ZIP error report files

set INPUT_FILE_PATTERN="*.zip"
set ACCEPTABLE_APPNAME="CrashRpt Tests"
set ACCEPTABLE_APPVERSION="1.2.0"
set SYM_SEARCH_DIRS="D:\Projects\CrashRpt\CrashRptSaved\1.2.0"
set SAVE_RESULTS_TO_DIR="valid_reports/"

mkdir %SAVE_RESULTS_TO_DIR%

for %%f in ( %INPUT_FILE_PATTERN% ) do call :process_report "%%f"
erase temp.txt
exit 

:process_report

  if %ACCEPTABLE_APPNAME%=="" goto appname_ok
  rem Get application name from the crash report file and write it to "temp.txt"
  crprober.exe /f %1 /o "temp.txt" /get XmlDescMisc AppName 0
  if not %errorlevel%==0 goto failed
 
  set /p app_name=<temp.txt
  if "%app_name%"==%ACCEPTABLE_APPNAME% goto appname_ok
  goto done

:appname_ok

  if %ACCEPTABLE_APPVERSION%=="" goto appversion_ok
  rem Get application version from the crash report file and write it to "temp.txt"
  crprober.exe /f %1 /o "temp.txt" /get XmlDescMisc AppVersion 0
  if not %errorlevel%==0 goto failed
 
  set /p app_version=<temp.txt
  if "%app_version%"==%ACCEPTABLE_APPVERSION% goto appversion_ok

  goto done

:appversion_ok

  rem Process report and write results to text file
  crprober.exe /f %1 /o %1.txt /sym %SYM_SEARCH_DIRS% 
  echo Return code=%errorlevel%
  if not %errorlevel%==0 goto failed

:ok
  move %1 %SAVE_RESULTS_TO_DIR%
  move %1.txt %SAVE_RESULTS_TO_DIR%
  goto done

:failed


:done
  
rem Return from subroutine
goto :eof

\endcode
*/
